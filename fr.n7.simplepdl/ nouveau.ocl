import 'SimplePDL.ecore'

package simplepdl

-- Contrainte 1: Deux activités différentes d'un même processus ne peuvent pas avoir le même nom
context WorkDefinition
inv uniqueWorkDefinitionName('Le nom de l''activité "' + self.name + '" n''est pas unique dans le processus'):
	self.process().processElements
		->select(e | e.oclIsKindOf(WorkDefinition))
		->collect(e | e.oclAsType(WorkDefinition))
		->select(wd | wd.name = self.name)
		->size() = 1

-- Contrainte 2: Une dépendance ne peut pas être réflexive
context WorkSequence
inv notReflexive('Une dépendance ne peut pas être réflexive: ' + self.predecessor.name + ' -> ' + self.successor.name):
	self.predecessor <> self.successor

-- Contrainte 3: Le nom d'une activité doit être composé d'au moins deux caractères
context WorkDefinition
inv minimumNameLength('Le nom de l''activité doit contenir au moins 2 caractères: ' + self.name):
	self.name.size() >= 2

-- Contrainte 4: Le nom d'une activité ne doit être composé que de lettres, chiffres ou soulignés, 
-- un chiffre ne peut pas être en première position
context WorkDefinition
inv validNameFormat('Le nom de l''activité ne respecte pas le format requis: ' + self.name):
	self.name.matches('[A-Za-z_][A-Za-z0-9_]*')

-- Définition de l'opération utilitaire pour récupérer le processus parent
context ProcessElement
def: process(): Process =
	Process.allInstances()
		->select(p | p.processElements->includes(self))
		->asSequence()->first()

endpackage
